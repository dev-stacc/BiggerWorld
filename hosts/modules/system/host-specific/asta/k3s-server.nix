{ config, pkgs, tailnet, ... } : {
    sops = {
        age.sshKeyPaths = [ "/etc/ssh/ssh_host_ed25519_key" ];
        secrets.k3s-token = {
            sopsFile = ../../../../../secrets/secrets.yaml;
        };
    };

    services.k3s = {
        enable = true;
        role = "server";
        tokenFile = config.sops.secrets.k3s-token.path;
        extraFlags = toString [
            "--disable traefik"
            "--node-ip=${tailnet.ips.asta}"
            "--advertise-address=${tailnet.ips.asta}"
            "--tls-san=asta"
            "--tls-san=${tailnet.ips.asta}"
            "--flannel-iface=tailscale0"
        ];
    };

    boot = {
        kernel.sysctl."net.ipv4.ip_forward" = 1;
        kernelModules = [ "br_netfilter"  "overlay" ];
        supportedFilesystems = [ "nfs" "nfs4" ];
    };

    # cni0/flannel.1 are needed for pods ON asta: they reach the API at asta's own
    # node IP, which is delivered locally rather than arriving over tailscale0.
    networking.firewall.interfaces = {
        tailscale0 = {
            allowedTCPPorts = [ 6443 10250 ];
            allowedUDPPorts = [ 8472 ];
        };
        cni0.allowedTCPPorts = [ 6443 10250 ];
        "flannel.1".allowedTCPPorts = [ 6443 10250 ];
    };

    environment = {
        variables.KUBECONFIG = "/etc/rancher/k3s/k3s.yaml";
        systemPackages = [
            pkgs.kubectl
        ];
    };

    systemd.services.k3s-kubeconfig-permissions = {
        description = "Fix k3s kubeconfig permissions";
        after = [ "k3s.service" ];
        wantedBy = [ "multi-user.target" ];
        serviceConfig = {
            Type = "oneshot";
            RemainAfterExit = true;
            ExecStart = "${pkgs.coreutils}/bin/chmod 644 /etc/rancher/k3s/k3s.yaml";
        };
    };
}
